%macro moran(tab,var,matrizw,tipo=);
proc iml;
use &tab var{&var};
read all into y;
use &matrizw;
read all into w;
n=nrow(y);
wy=w*y;
ym=sum(y)/n;
wym=sum(wy)/n;
wy=wy-wym;
y=y-ym;
b=inv(y`*y)*y`*wy;
create b var{b};
append;
create wy var{y wy};
append;
quit;
proc sql noprint;
create table wy1 as
select max(y) as maxy, min(y) as miny
from wy;
create table wy2 as
select max(wy) as maxwy, min(wy) as minwy
from wy;
select maxy into: my from wy1;
select miny into: mny from wy1;
select maxwy into: mwy from wy2;
insert into wy1
values (0,&my);
insert into wy2
values (0,&mwy);
update wy1
set maxy=0 where maxy>0;
update wy2
set maxwy=0 where maxwy>0;
quit;
data b;set b;
a=atan(b);
aa=a/0.01745;
ax=&my*tan(a);
_ax=&mny*tan(a);
run;
data _null_;
set b;
call symput('b',trim(left(round(b,0.01))));
call symput('a',trim(left(round(aa,0.01))));
call symput('ax',ax);
call symput('_ax',_ax);
run;
data wy;
merge wy wy1 wy2;
if _n_=1 then do;
xxl=&my;
xxm=&ax;
end;
if _n_=2 then do;
xxl=&mny;
xxm=&_ax;
end;
run;
proc greplay igout=work.gseg nofs;
delete mor_fin moran_p moran br;
run;
quit;
goptions reset=global ftitle='Verdana' ftext='Verdana';
title1 "Moran Scatterplot ";
title2 "vari�vel &var";
proc gplot data=wy;
plot wy*y maxy*miny minwy*maxwy xxm*xxl / overlay name='moran_p' href=0 vref=0;
symbol1 v=dot c=blue;
symbol2 v=point c=black i=j;
symbol3 v=point c=black i=j;
symbol4 v=point c=black i=j;
footnote1 j=l c=red "b=&b / alpha=&a.º";
run;
quit;
data &tab;
merge &tab wy;
if y>0 and wy>0 then I='High-High';
if y<0 and wy>0 then I='Low-High';
if y>0 and wy<0 then I='High-Low';
if y<0 and wy<0 then I='Low-Low';
run;
proc freq data=&tab noprint;
tables I /out=freq(drop=count);
run;
data freq;set freq;
if I='High-High' then I2='HH';
if I='High-Low' then I2='HL';
if I='Low-High' then I2='LH';
if I='Low-Low' then I2='LL';
run;
data _null_;set freq;
call symput('h'||trim(left(I2)),trim(left(round(percent,0.01)))||trim(left('%')));
call symput('name'||trim(left(_n_)),trim(left(I)));
run;
proc sql noprint;select count(i) into:n1 from freq where i='High-High';quit;
proc sql noprint;select count(i) into:n2 from freq where i='High-Low';quit;
proc sql noprint;select count(i) into:n3 from freq where i='Low-High';quit;
proc sql noprint;select count(i) into:n4 from freq where i='Low-Low';quit;
proc sql noprint;select count(*) into:nfreq from freq ;quit;
%if &n1=1 %then %do;pattern1 c=red;%end;
%if &n2=1 %then %do;pattern2 c=blue;%end;
%if &n3=1 %then %do;pattern3 c=vpab;%end;
%if &n4=1 %then %do;pattern4 c=pink;%end;
goptions ftitle='Verdana' ftext='Verdana';
title1 "Moran Scatterplot ";
title2 "vari�vel &var";
%if %upcase(&tipo)=MICRO %then %do;
%let t=brasilmicro;
%let t1=microcod;
%end;
%else %if %upcase(&tipo)=MUN %then %do;
%let t=brasil;
%let t1=codigo;
%end;
%else %if %upcase(&tipo)=MESO %then %do;
%let t=brasilmeso;
%let t1=mesocod;
%end;
%else %do;
%let t=&tipo;
%let t1=&codigo;
%end;
proc gmap data=&tab map=&t all;
id &t1;
choro i / cempty=white name='moran';
%if &n1=0 %then %let hHH=0%;
%if &n2=0 %then %let hHL=0%;
%if &n3=0 %then %let hLH=0%;
%if &n4=0 %then %let hLL=0%;
footnote1 c=red "High-High=&hHH  High-Low=&hHL";
footnote2 c=red "Low-High=&hLH  Low-Low=&hLL";
run;
quit;
goptions ftitle='Verdana' ftext='Verdana';
pattern v=s c=white repeat=4;
title1 "Moran Scatterplot ";
title2 "vari�vel &var";
proc gmap data=&tab
%if %upcase(&tipo)=MESO or %upcase(&tipo)=MUN or %upcase(&tipo)=MICRO %then map=brasilest;
%else map=&t; all;
id &t1;
choro i / cempty=black name='br';
footnote1 c=red "High-High=&hHH  High-Low=&hHL";
footnote2 c=red "Low-High=&hLH  Low-Low=&hLL";
run;
quit;
proc greplay igout=work.gseg gout=work.gseg nofs tc=sashelp.templt template=whole;
treplay 1:br 1:moran name='mor_fin';
run;
quit;
%mend moran;
